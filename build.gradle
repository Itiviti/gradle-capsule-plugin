plugins {
  id "com.gradle.plugin-publish" version "0.9.1"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

def project_description = """\
A Gradle plugin utilizing Capsule, the packaging and deployment tool for JVM apps.
(http://www.capsule.io)
"""

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
}

pluginBundle {
  website = 'https://github.com/danthegoodman/gradle-capsule-plugin'
  vcsUrl = 'https://github.com/danthegoodman/gradle-capsule-plugin.git'
  description = project_description
  tags = ['shade', 'fatjar', 'uberjar', 'onejar', 'capsule']

  plugins {
    capsulePlugin {
      id = 'us.kirchmeier.capsule'
      displayName = 'Gradle Capsule Plugin'
    }
  }

  mavenCoordinates {
    groupId = 'us.kirchmeier'
    artifactId = "gradle-capsule-plugin"
  }
}

test {
    useTestNG()
}

task testIntegration(dependsOn:'jar', group: 'Verification', description: 'Runs the integration tests') << {
  println("--------------------------------------------------------------------------------")
  def baseDir = "${projectDir}/src/test/gradle"

  def proc = ["${baseDir}/gradlew", "clean", "self-test"].execute(null, file(baseDir))
  proc.consumeProcessOutput(System.out, System.err)
  int exitCode = proc.waitFor()
  println("--------------------------------------------------------------------------------")
  if(exitCode != 0){
    throw new GradleException("Integration Tests Failed")
  }
}

check.dependsOn testIntegration
